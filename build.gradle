/*
 *  /***************************************************************************
 *  Copyright (c) 2017, EPAM SYSTEMS INC
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *     http://www.apache.org/licenses/LICENSE-2.0
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 *  ***************************************************************************
 */
plugins {
    id 'java-library'
    id 'groovy'
    id 'maven'
    id 'signing'
    id 'io.codearte.nexus-staging' version '0.21.2'
}

apply plugin: 'maven-publish'

group group
version version

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()

    maven {
        url "https://m2proxy.atlassian.com/repository/public"
    }
    maven {
        url 'https://maven.atlassian.com/content/repositories/atlassian-public'
    }
    maven {
        url 'http://maven.jahia.org/maven2'
    }

    maven {
        url 'https://artifacts.elastic.co/maven'
    }

    maven {
        url "https://oss.sonatype.org/content/groups/public"
    }
}

dependencies {
    api "com.epam.dep.esp:gmp-common:$gmpCommonVersion"
    api "org.codehaus.groovy:groovy-all:$groovyVersion"
    api "org.slf4j:slf4j-api:${slf4jVersion}"
    api "org.slf4j:slf4j-log4j12:${slf4jVersion}"
    api "com.atlassian.jira:jira-rest-java-client-api:$atlassianJiraRestVersion"
    api "com.atlassian.jira:jira-rest-java-client-core:$atlassianJiraRestVersion"
    api "io.atlassian.fugue:fugue:$atlassianFugueVersion"
    api "com.fasterxml.jackson.core:jackson-databind:$jacksonDatabindVersion"
    api "org.apache.commons:commons-io:$commonsIoVersion"
    api "commons-cli:commons-cli:$commonsCliVersion"
    api "org.json:json:$jsonVersion"
    api "net.sf.ehcache:ehcache:$ehCacheVersion"

//ElasticSearch
    implementation "org.elasticsearch.client:transport:${elasticSearchVersion}"
    implementation "org.apache.logging.log4j:log4j-api:$log4jVersion"
    implementation "org.apache.logging.log4j:log4j-core:$log4jVersion"
    implementation "org.elasticsearch.client:x-pack-transport:${elasticSearchVersion}"
//end ElasticSearch

    testImplementation "junit:junit:$junitVersion"
}

task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.runtime
}

jar {
    manifest {
        attributes("Implementation-Title": "${rootProject.group}:${rootProject.name}",
                "Implementation-Version": version,
                "Implementation-Vendor": "EPAM SYSTEMS INC")
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }

            pom.project {
                packaging 'jar'
                name 'gmp-tools'
                description 'Tools module for GroovyManiPulator'
                url 'https://github.com/epam/gmp-tools'

                scm {
                    url 'https://github.com/epam/gmp-tools'
                    connection 'scm:https://github.com/epam/gmp-tools.git'
                    developerConnection 'scm:https://github.com/epam/gmp-tools.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'asamuta'
                        name 'Andrei Samuta'
                        email 'a.samuta@gmail.com'
                    }
                }
            }
        }
    }
}

nexusStaging {
    packageGroup = "com.epam.dep"
    numberOfRetries = 40
    delayBetweenRetriesInMillis = 10000
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

if (project.hasProperty('signing.keyId')) {
    signing {
        sign configurations.archives
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.epam.dep.esp'
            artifactId = 'gmp-tools'
            version = 'DEV-SNAPSHOT'

            from components.java
        }
    }
}
