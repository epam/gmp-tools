/*
 *  /***************************************************************************
 *  Copyright (c) 2017, EPAM SYSTEMS INC
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *     http://www.apache.org/licenses/LICENSE-2.0
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 *  ***************************************************************************
 */

group group
version version

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()

    maven {
        url "https://m2proxy.atlassian.com/repository/public"
    }
    maven {
        url 'https://maven.atlassian.com/content/repositories/atlassian-public'
    }
    maven {
        url 'http://maven.jahia.org/maven2'
    }

    maven {
        url 'https://artifacts.elastic.co/maven'
    }

    maven {
        url "https://oss.sonatype.org/content/groups/public"
    }
}

dependencies {
    compile "com.epam.dep.esp:gmp-common:$gmpCommonVersion"
    compile "org.codehaus.groovy:groovy-all:$groovyVersion"
    compile "org.slf4j:slf4j-api:${slf4jVersion}"
    compile "org.slf4j:slf4j-log4j12:${slf4jVersion}"
    compile "com.atlassian.jira:jira-rest-java-client-api:$atlassianJiraRestVersion"
    compile "com.atlassian.jira:jira-rest-java-client-core:$atlassianJiraRestVersion"
    compile "com.atlassian.fugue:fugue:$atlassianFugueVersion"
    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonDatabindVersion"
    compile "org.apache.commons:commons-io:$commonsIoVersion"
    compile "commons-cli:commons-cli:$commonsCliVersion"
    compile "org.json:json:$jsonVersion"

    compile "net.sf.ehcache:ehcache:$ehCacheVersion"

//ElasticSearch
    compile "org.elasticsearch.client:transport:${elasticSearchVersion}"
    compile "org.apache.logging.log4j:log4j-api:$log4jVersion"
    compile "org.apache.logging.log4j:log4j-core:$log4jVersion"
    compile "org.elasticsearch.client:x-pack-transport:${elasticSearchVersion}"
//end ElasticSearch

    testCompile "junit:junit:$junitVersion"
}

task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.runtime
}

jar {
    manifest {
        attributes("Implementation-Title": "${rootProject.group}:${rootProject.name}",
                "Implementation-Version": version,
                "Implementation-Vendor": "EPAM SYSTEMS INC")
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }

            pom.project {
                packaging 'jar'
                name 'gmp-tools'
                description 'Tools module for GroovyManiPulator'
                url 'https://github.com/epam/gmp-tools'

                scm {
                    url 'https://github.com/epam/gmp-tools'
                    connection 'scm:https://github.com/epam/gmp-tools.git'
                    developerConnection 'scm:https://github.com/epam/gmp-tools.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'asamuta'
                        name 'Andrei Samuta'
                        email 'a.samuta@gmail.com'
                    }
                }
            }
        }
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

if (project.hasProperty('signing.keyId')) {
    signing {
        sign configurations.archives
    }
}